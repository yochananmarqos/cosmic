## WIP

pkgname=cosmic-reader-git
pkgver=r23.c54e8ec
pkgrel=1
pkgdesc="WIP COSMIC PDF reader"
arch=('x86_64' 'aarch64')
url="https://github.com/pop-os/cosmic-reader"
license=('GPL-3.0-or-later')
#groups=('cosmic')
depends=('libxkbcommon')
makedepends=('cargo' 'git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/pop-os/cosmic-reader.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  # use nice to build with lower priority
  nice cargo build --release --frozen
}

package() {
  cd "${pkgname%-git}"
  install -Dm755 "target/release/${pkgname%-git}" -t "$pkgdir/usr/bin/"
}
