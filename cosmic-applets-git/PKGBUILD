# https://aur.archlinux.org/packages/cosmic-applets-git

pkgname=cosmic-applets-git
pkgver=r741.b7c6cc0
pkgrel=1
pkgdesc="WIP applets for cosmic-panel"
arch=('x86_64' 'aarch64')
url="https://github.com/pop-os/cosmic-applets"
license=('GPL-3.0-or-later')
groups=('cosmic')
depends=(
#  'cosmic-icons-git'
#  'cosmic-panel-git'
  'dbus'
  'hicolor-icon-theme'
  'libxkbcommon'
  'pulse-native-provider'
  'systemd-libs'
)
makedepends=(
  'cargo'
  'git'
  'just'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=('!lto')
source=('git+https://github.com/pop-os/cosmic-applets.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname%-git}"
  export CARGO_HOME="$srcdir/cargo-home"
  export RUSTUP_TOOLCHAIN=stable
  just vendor='1' build-release
}

package() {
  cd "${pkgname%-git}"
  just rootdir="$pkgdir" install
}
