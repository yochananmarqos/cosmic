## WIP

# "We aren't planning to ship cosmic-player with the alpha release, 
# I would not recommend to include it. It has performance issues 
# due to having a GPU to CPU transfer of frames followed by 
# CPU conversion from YUV to RGB0"
# -- Jeremy Soller

pkgname=cosmic-player-git
pkgver=r80.d8ee67c
pkgrel=1
pkgdesc="WIP COSMIC media player"
arch=('x86_64' 'aarch64')
url="https://github.com/pop-os/cosmic-player"
license=('GPL-3.0-or-later')
depends=(
  'gstreamer'
  'gst-plugins-good'
  'gst-plugins-base'
  'libxkbcommon'
)
makedepends=(
  'cargo'
  'clang'
  'git'
  'just'
  'mold'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/pop-os/cosmic-player.git')
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname%-git}"
  export RUSTUP_TOOLCHAIN=stable

  # use mold instead of lld to speed up build
  RUSTFLAGS+=" -C link-arg=-fuse-ld=mold"

  # use nice to build with lower priority
  nice just build-release --frozen
}

package() {
  cd "${pkgname%-git}"
  just rootdir="$pkgdir" install
}
